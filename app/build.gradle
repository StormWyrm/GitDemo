apply plugin: "com.android.application"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    signingConfigs {
        def appStorePassword = System.getenv("store password")
        def appKeyAlias = System.getenv("key alias")
        def appKeyPassword = System.getenv("key password")

        if(!appStorePassword || !appKeyAlias || !appKeyPassword){
            appStorePassword = "123456"
            appKeyAlias = 'key0'
            appKeyPassword = '123456'
        }

        vip {
            storeFile file('../vip_key.jks')
            storePassword appStorePassword
            keyAlias appKeyAlias
            keyPassword appKeyPassword
        }
        free {
            storeFile file('../free_key.jks')
            storePassword appStorePassword
            keyAlias appKeyAlias
            keyPassword appKeyPassword
        }
    }

    flavorDimensions 'channel', 'pay'
    productFlavors {
        normal {
            dimension 'channel'
        }
        xiaomi {
            dimension 'channel'
            applicationIdSuffix 'xiaomi'
        }
        baidu {
            dimension 'channel'
            applicationIdSuffix 'baidu'
        }

        vip {
            dimension 'pay'
            buildConfigField("boolean", "isFreeVersion", 'false')
            signingConfig signingConfigs.vip
        }

        free {
            dimension 'pay'
            buildConfigField("boolean", "isFreeVersion", 'true')
            signingConfig signingConfigs.free
        }
    }

    defaultConfig {
        applicationId 'com.github.StormWyrm.GitDemo'
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName '1.0.0'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}